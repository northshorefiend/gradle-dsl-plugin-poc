plugins {
    id 'java-library'
    id 'checkstyle'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// https://github.com/gradle/gradle/issues/27035
configurations.checkstyle {
    resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
        select("com.google.guava:guava:0")
    }
}

checkstyle {
    configFile file("${rootDir}/build/tmp/google_checks.xml")
    toolVersion '10.12.6'
}

// The below two stanzas copy the checkstyle config from the resources of the plugin to the build dir of the project
// You can use this to put in place any resources needed for the tasks you are configuring in the plugin
tasks.register('copyCheckStyleConfig', Copy) {
    from resources.text.fromArchiveEntry(buildscript.configurations.classpath, "google_checks.xml").asFile()
    into 'build/tmp'
}

tasks.named('checkstyleMain') { dependsOn('copyCheckStyleConfig') }

// The below is needed as we are copying to the build dir that is also used by the jar task
tasks.named('jar') { dependsOn('copyCheckStyleConfig') }
