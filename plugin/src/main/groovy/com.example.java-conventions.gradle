plugins {
    id 'java-library'
    id 'checkstyle'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// https://github.com/gradle/gradle/issues/27035
configurations.checkstyle {
    resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
        select("com.google.guava:guava:0")
    }
}

checkstyle {
    configFile file("${rootDir}/build/myConfig/google_checks.xml")
    toolVersion '10.12.6'
}

// these two stanzas copy the config file from the plugin resources to the project build dir ready for the above
// checkstyle config

tasks.register('copyCheckStyleConfig') {
    outputs.file('build/myConfig/google_checks.xml')
    doLast {
        copy {
            from resources.text.fromArchiveEntry(buildscript.configurations.classpath, "google_checks.xml").asFile()
            into 'build/myConfig'
        }
    }
}

tasks.named('checkstyleMain') {
    dependsOn('copyCheckStyleConfig')
}
